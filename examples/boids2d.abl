/* Copyright 2017 OpenABL Contributors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License. */

agent Boid {
  position float2 pos;
  float2 velocity;
}

param float interaction_radius = 0.05;
param float separation_radius = 0.005;
param float agent_density = 500;

float time_scale = 0.0005;
float global_scale = 0.15;

float steer_scale = 0.65;
float collision_scale = 0.75;
float match_scale = 1.25;

param int num_agents = 5000;
param int num_timesteps = 100;

float min_pos = 0;
float max_pos = sqrt(num_agents/agent_density);

environment {
  min: float2(min_pos),
  max: float2(max_pos)
}

// This function looks somewhat weird to me, but let's take it as-is
float2 boundPosition(float2 pos) {
  return float2(
    pos.x < min_pos ? max_pos :
    pos.x > max_pos ? min_pos : pos.x,
    pos.y < min_pos ? max_pos :
    pos.y > max_pos ? min_pos : pos.y
  );
}

step update_boid(Boid in->out) {
  float2 global_velocity = float2(0, 0);
  float2 global_center = float2(0, 0);
  float2 collision_center = float2(0, 0);
  int interaction_count = 0;
  int collision_count = 0;

  for (Boid nx : near(in, interaction_radius)) {
    global_center += nx.pos;
    global_velocity += nx.velocity;
    interaction_count += 1;

    float separation = dist(in.pos, nx.pos);
    if (separation < separation_radius) {
      collision_center += nx.pos;
      collision_count += 1;
    }
  }

  float2 velocity_change = float2(0, 0);

  // Rule 1) Steer towards perceived center of flock
  float2 steer_velocity = float2(0, 0);
  if (interaction_count > 0) {
    global_center /= interaction_count;
    steer_velocity = (global_center - in.pos) * steer_scale;
  }
  velocity_change += steer_velocity;

  // Rule 2) Match neighbours speeds
  float2 match_velocity = float2(0, 0);
  if (interaction_count > 0) {
    global_velocity /= interaction_count;
    match_velocity = global_velocity * match_scale;
  }
  velocity_change += match_velocity;

  // Rule 3) Avoid close range neighbors
  float2 avoid_velocity = float2(0, 0);
  if (collision_count > 0) {
    collision_center /= collision_count;
    avoid_velocity = (in.pos - collision_center) * collision_scale;
  }
  velocity_change += avoid_velocity;

  float2 new_velocity = in.velocity + velocity_change * global_scale;
  float new_velocity_scale = length(new_velocity);
  if (new_velocity_scale > 1) {
    new_velocity /= new_velocity_scale;
  }

  float2 new_pos = in.pos + new_velocity * time_scale;
  new_pos = boundPosition(new_pos);

  out.pos = new_pos;
  out.velocity = new_velocity;
}

void main() {
  //load("boids.in");

  for (int i : 0..num_agents) {
    add(Boid {
      pos: random(float2(min_pos), float2(max_pos)),
      velocity: random(float2(-1), float2(1)),
    });
  }

  simulate(num_timesteps) { update_boid }

  save("boids.out");
}
